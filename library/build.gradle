
plugins {
    id 'maven-publish'
    id 'signing'
}

apply plugin: 'com.android.library'

version = '1.3.0'
def libName = "numberpickerview"

android {
    compileSdkVersion 34

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 34
        versionCode 1
        versionName "${project.version}"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    // seems as if recent AGP version have borked output file name generation
    libraryVariants.all { variant ->
        variant.outputs.all {
            if ("release".equals(variant.name)) {
                outputFileName = "${libName}-${project.version}.aar"
            } else {
                outputFileName = "${libName}-${project.version}-${variant.name}.aar"
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}

android.libraryVariants.all { variant ->
    task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
        description "Generates Javadoc for $variant.name."
        group "Documentation"
        source = variant.javaCompile.source
        doFirst {
            classpath = files(variant.javaCompile.classpath.files) + files(android.bootClasspath) + files(variant.javaCompile.destinationDir)
        }
        options.links("http://docs.oracle.com/javase/7/docs/api/");
        options.links("http://d.android.com/reference/");
        exclude '**/BuildConfig.java'
        exclude '**/R.java'
    }
}

ext {
    // hardwiring this is ugly but it does work
    releaseSource = file('src/main/java/ch/poole/android/numberpickerview')
    releaseJavadoc = file('build/docs/javadoc')
}

task sourcesJar(type: Jar) {
    from releaseSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: 'generateReleaseJavadoc') {
    classifier = 'javadoc'
    from releaseJavadoc
    //  options.encoding = 'UTF-8'
}

ext {
    repoUrl = 'https://github.com/simonpoole/numberpickerview.git'
    spdxId = 'Apache-2.0'
}

publishing {
    publications {
        Release(MavenPublication) {
            groupId 'ch.poole.android'
            artifactId 'numberpickerview'
            artifact sourcesJar 
            artifact javadocJar 
            artifact("$buildDir/outputs/aar/${libName}-${project.version}.aar")
            pom {
                name = artifactId
                description = 'Another NumberPicker with more flexible attributes on Android platform.' 
                url = 'https://github.com/simonpoole/numberpickerview'
                scm {
                    url = repoUrl
                }
                licenses {
                    license {
                        name = spdxId
                        url = 'https://raw.githubusercontent.com/simonpoole/numberpickerview/master/LICENCE'
                    }
                }
                developers {
                    developer {
                        name='Carbs.Wang'
                        organization='https://github.com/Carbs0126'
                    }
                    developer {
                        name = 'Simon Poole'
                    }
                }
            }
            repositories {
                maven {
                    name = 'sonatype'
                    credentials {
                        username System.getenv('SONATYPE_USER')
                        password System.getenv('SONATYPE_PASSWORD')
                    }
                    def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                    def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                    url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                }
            }
        }
    }
}

signing {
    sign publishing.publications.Release
}
